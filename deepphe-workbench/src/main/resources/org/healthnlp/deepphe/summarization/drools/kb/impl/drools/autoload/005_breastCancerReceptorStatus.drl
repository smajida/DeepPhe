package org.healthnlp.deepphe.summarization.drools.rules;

//list any import classes here.
import org.healthnlp.deepphe.summarization.drools.kb.*;
import org.healthnlp.deepphe.summarization.drools.kb.impl.*;

//
// Receptor status - Hormone receptor negative
//
// If ER-, PR- => phenotype receptor status = Hormone Receptor Negative
//

//
// ER - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with ER relation <ER, value>
//  => phenotype ER = value

rule "005_breastCancerReceptorStatus Er"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         EstrogenReceptorStatus($erId : id, summarizableId == $encounterId)
         $ord : OrdinalInterpretation($ordId : id, summarizableId == $encounterId)   
         RelationHasinterpretation($erId == domainId, $ordId == rangeId)    
         not EstrogenReceptorStatus(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Estrogen Receptor Fires... ");
        EstrogenReceptorStatus patientEr = new EstrogenReceptorStatusImpl();
        OrdinalInterpretation patientOrdinalInterpretation = beanCopyOrdinalInterpretation($ord);
        RelationHasinterpretation patientRelationHasInterpretation = new RelationHasinterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientEr.getId());
        patientRelationHasInterpretation.setRangeId(patientOrdinalInterpretation.getId());
        
        patientEr.setSummarizableId($patientId);
        patientOrdinalInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
       
        insert(patientEr);
        insert(patientOrdinalInterpretation);
        insert(patientRelationHasInterpretation);
    end
    
//
// PR - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with PR relation <PR, value>
//  => phenotype PR = value
//
rule "005_breastCancerReceptorStatus Pr"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         ProgesteroneReceptorStatus($prId : id, summarizableId == $encounterId)
         $ord : OrdinalInterpretation($ordId : id, summarizableId == $encounterId)   
         RelationHasinterpretation($prId == domainId, $ordId == rangeId)    
         not ProgesteroneReceptorStatus(summarizableId == $patientId)          
    then
        System.out.println("<DROOLS-ENGINE> Progesterone Receptor Fires... ");
        ProgesteroneReceptorStatus patientPr = new ProgesteroneReceptorStatusImpl();
        OrdinalInterpretation patientOrdinalInterpretation = beanCopyOrdinalInterpretation($ord);
        RelationHasinterpretation patientRelationHasInterpretation = new RelationHasinterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientPr.getId());
        patientRelationHasInterpretation.setRangeId(patientOrdinalInterpretation.getId());
                       
        patientPr.setSummarizableId($patientId);
        patientOrdinalInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
            
        insert(patientPr);
        insert(patientOrdinalInterpretation);
        insert(patientRelationHasInterpretation);
    end
    
//
// Her2 - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with Her2 relation <Her2, value>
//  => phenotype Her2 = value  
  
rule "005_breastCancerReceptorStatus Her2"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         Her2NeuStatus($herNeuId : id, summarizableId == $encounterId)
         $ord : OrdinalInterpretation($ordId : id, summarizableId == $encounterId)   
         RelationHasinterpretation($herNeuId == domainId, $ordId == rangeId)    
         not Her2NeuStatus(summarizableId == $patientId)         	           
    then
        System.out.println("<DROOLS-ENGINE> Her2/Neu Receptor Fires... ");
        Her2NeuStatus patientHer2 = new Her2NeuStatusImpl();
        OrdinalInterpretation patientOrdinalInterpretation = beanCopyOrdinalInterpretation($ord);
        RelationHasinterpretation patientRelationHasInterpretation = new RelationHasinterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientHer2.getId());
        patientRelationHasInterpretation.setRangeId(patientOrdinalInterpretation.getId());
 
        patientHer2.setSummarizableId($patientId);
        patientOrdinalInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
 
        insert(patientHer2);
        insert(patientOrdinalInterpretation);
        insert(patientRelationHasInterpretation);
    end
 
//
// Receptor status triple positive
//
// If ER+, PR+, HER2+ => phenotype receptor status = Triple Positive
//
rule "005_breastCancerReceptorStatus Triple Positive"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Her2NeuStatus($her2NeuId : id, summarizableId == $patientId)         
         Positive($erPosId : id)
         Positive($prPosId : id)
         Positive($her2NeuPosId : id)
         RelationHasinterpretation(domainId == $erId, rangeId == $erPosId)
         RelationHasinterpretation(domainId == $prId, rangeId == $prPosId)
         RelationHasinterpretation(domainId == $her2NeuId, rangeId == $her2NeuPosId)
         not TriplePositive(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Triple Positive Fires... ");
        TriplePositive patientTriplePositive = new TriplePositiveImpl();
        patientTriplePositive.setSummarizableId($patientId);
        insert(patientTriplePositive);
    end
    
//
// Receptor status triple negative
//
// If ER-, PR-, HER2- => phenotype receptor status = Triple Negative
//
rule "005_breastCancerReceptorStatus Triple Negative"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Her2NeuStatus($her2NeuId : id, summarizableId == $patientId)         
         Negative($erPosId : id)
         Negative($prPosId : id)
         Negative($her2NeuPosId : id)
         RelationHasinterpretation(domainId == $erId, rangeId == $erPosId)
         RelationHasinterpretation(domainId == $prId, rangeId == $prPosId)
         RelationHasinterpretation(domainId == $her2NeuId, rangeId == $her2NeuPosId)
         not TripleNegative(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Triple Negative Fires... ");
        TripleNegative patientTripleNegative = new TripleNegativeImpl();
        patientTripleNegative.setSummarizableId($patientId);
        insert(patientTripleNegative);
    end
    
//
// Receptor status - Hormone receptor negative
//
// If ER-, PR- => phenotype receptor status = Hormone Receptor Negative
//
rule "005_breastCancerReceptorStatus Hormone receptor negative"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Negative($erPosId : id)
         Negative($prPosId : id)
         RelationHasinterpretation(domainId == $erId, rangeId == $erPosId)
         RelationHasinterpretation(domainId == $prId, rangeId == $prPosId)
         not TripleNegative(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Hormone receptor negative fires... ");
        TripleNegative patientTripleNegative = new TripleNegativeImpl();
        patientTripleNegative.setSummarizableId($patientId);
        insert(patientTripleNegative);
    end    

//
// Retract the goal for this module
//
rule "005_breastCancerReceptorStatus Transition Goal State"
	salience 500
	when
	    $g : KbGoal (name == "extract-receptor-status",
                isActive > 0)
    then
        System.out.println("<DROOLS-ENGINE> Achieved goal " + $g.getName());
    	retract($g);
    end
    
function OrdinalInterpretation beanCopyOrdinalInterpretation(OrdinalInterpretation ordinalInterpretation) {
        return (OrdinalInterpretation) beanCopy(ordinalInterpretation);
        }    
