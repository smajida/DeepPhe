package org.healthnlp.deepphe.summarization.drools.rules;

//list any import classes here.
import org.healthnlp.deepphe.summarization.drools.kb.*;
import org.healthnlp.deepphe.summarization.drools.kb.impl.*;

// ==========================================================
// Stage0-1	If document has pTis, pN0, pM0 then Stage 0 Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage0-1-1"
	salience 3800
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
        Tis (summarizableId == $patientId)
        Pn0 (summarizableId == $patientId)
        M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-1-1 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
//Stage0-2	If document has primary dx ONLY DCIS OR (DCIS & Pagets), then Stage 0 Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage0-2-1"
	salience 3850
	when
        KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
        Tumor ($tumorId : id, summarizableId == $patientId)
        Ductal ($histologyId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $tumorId, rangeId == $histologyId)
        InSitu ($inSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $tumorId, rangeId == $inSituId)
        not (
             Tumor ($anotherTumorId : id, summarizableId == $patientId)
            and Invasive($invasiveId : id, summarizableId == $patientId)
            and HasTumorExtent(domainId == $anotherTumorId, rangeId == $invasiveId)
              )        
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-2-1 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
  
rule "004_breastCancerStage Stage0-2-2"
	salience 3900
	when
        KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
        Tumor ($tumorId : id, summarizableId == $patientId)
        Ductal($histologyId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $tumorId, rangeId == $histologyId)
        InSitu ($inSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $tumorId, rangeId == $inSituId)
        PagetsDisease ($pagetsId : id,  summarizableId == $patientId)
        not (
           Tumor ($invasiveTumorId : id, summarizableId == $patientId)
           and Invasive ($invasiveId : id, summarizableId == $patientId)
           and HasTumorExtent(domainId == $invasiveTumorId, rangeId == $invasiveId)
        )
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-2-2 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end

// ==========================================================
//
//Stage0-3	If document has primary dx ONLY LCIS OR (LCIS & Pagets),  then Stage 0 Breast 
//
// ==========================================================
rule "004_breastCancerStage Stage0-3-1"
	salience 3950
	when
        KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
        Tumor ($tumorId : id, summarizableId == $patientId)
        Lobular($histologyId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $tumorId, rangeId == $histologyId)
        InSitu ($inSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $tumorId, rangeId == $inSituId)
        not (Tumor ($invasiveTumorId : id, summarizableId == $patientId)
         and HasTumorExtent(domainId == $invasiveTumorId, $extentId : rangeId)
         and Invasive (id == $extentId, summarizableId == $patientId))
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-3-1 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
rule "004_breastCancerStage Stage0-3-2"
	salience 4000
	when
        KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
        Tumor ($tumorId : id, summarizableId == $patientId)
        Lobular($histologyId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $tumorId, rangeId == $histologyId)
        InSitu ($inSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $tumorId, rangeId == $inSituId)
        PagetsDisease ($pagetsId : id,  summarizableId == $patientId)
        not (Tumor ($invasiveTumorId : id, summarizableId == $patientId)
        and HasTumorExtent(domainId == $invasiveTumorId, $extentId : rangeId)
        and Invasive (id == $extentId, summarizableId == $patientId))
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-3-2 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
//Stage0-4	If document has primary dx ONLY (DCIS & LCIS) OR (DCIS & LCIS & Paget's), then Stage 0 Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage0-4-1"
	salience 4050
	when
        KbGoal (name == "extract-stage", isActive > 0)
        KbPatient($patientId : id)
        Tumor ($dcisTumorId : id, summarizableId == $patientId)
        Ductal ($ductalId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $dcisTumorId, rangeId == $ductalId)
        InSitu ($dcisInSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $dcisTumorId, rangeId == $dcisInSituId)
        Tumor ($lcisTumorId : id, summarizableId == $patientId)
        Lobular ($lobularId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $lcisTumorId, rangeId == $lobularId)
        InSitu ($lcisInSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $lcisTumorId, rangeId == $lcisInSituId)
        not (
           Tumor ($invasiveTumorId : id, summarizableId == $patientId)
           and HasTumorExtent(domainId == $invasiveTumorId, $extentId : rangeId)
           and Invasive (id == $extentId, summarizableId == $patientId)
           )
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-4-1 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
rule "004_breastCancerStage Stage0-4-2"
	salience 4100
	when
        KbGoal (name == "extract-stage", isActive > 0)
        KbPatient($patientId : id)
        Tumor ($dcisTumorId : id, summarizableId == $patientId)
        Ductal ($ductalId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $dcisTumorId, rangeId == $ductalId)
        InSitu ($dcisInSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $dcisTumorId, rangeId == $dcisInSituId)
        Tumor ($lcisTumorId : id, summarizableId == $patientId)
        Lobular ($lobularId : id, summarizableId == $patientId)
        HasHistologicType(domainId == $lcisTumorId, rangeId == $lobularId)
        InSitu ($lcisInSituId : id, summarizableId == $patientId)
        HasTumorExtent(domainId == $lcisTumorId, rangeId == $lcisInSituId)
        PagetsDisease ($pagetsId : id,  summarizableId == $patientId)
        not (
           Tumor ($invasiveTumorId : id, summarizableId == $patientId)
           and HasTumorExtent(domainId == $invasiveTumorId, $extentId : rangeId)
           and Invasive (id == $extentId, summarizableId == $patientId)
           )
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-4-2 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
//Stage 0-5   If document has primary dx ONLY Paget's Disease, then Stage 0 Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage0-5-1"
	salience 4150
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    PagetsDisease ($pagetsId : id,  summarizableId == $patientId)
        not Tumor (id != $pagetsId, summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage0-5-1 Assigning Stage0 cancer for patient ");      
        CancerStage cancerStage = new Stage0Impl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage1a-1	If document has pT1, pN0, pM0, then Stage IA Breast Cancer	
//
// Note: This could be a little confusing - any M0 is clinical. Path cannot tell	
//
// ==========================================================
rule "004_breastCancerStage Stage1a-1-1"
	salience 4200
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T1 (summarizableId == $patientId)
	    Pn0 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage1a-1-1 Assigning Stage1a cancer for patient ");      
        CancerStage cancerStage = new StageiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage1b-1	If document has pT0, pN1mi, pM0, then Stage IB Breast Cancer
//	
// ==========================================================
rule "004_breastCancerStage Stage1b-1-1"
	salience 4250
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T0 (summarizableId == $patientId)
	    Pn1mi (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage1b-1-1 Assigning Stage1b cancer for patient ");      
        CancerStage cancerStage = new StageibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage1b-2	If document has pT1, pN1mi, pM0, then Stage IB Breast Cancer
//	
// ==========================================================
rule "004_breastCancerStage Stage1b-2-1"
	salience 4300
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T1 (summarizableId == $patientId)
	    Pn1mi (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage1b-2-1 Assigning Stage1b cancer for patient ");      
        CancerStage cancerStage = new StageibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage2a-1	If document has pT0, pN1, pM0, then Stage IIA Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage2a-1-1"
	salience 4350
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T0 (summarizableId == $patientId)
	    Pn1 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage2a-1-1 Assigning Stage2a cancer for patient ");      
        CancerStage cancerStage = new StageiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
    
// ==========================================================
//
// Stage2a-2	If document has pT1, pN1, pM0, then Stage IIA Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage2a-2-1"
	salience 4400
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T1 (summarizableId == $patientId)
	    Pn1 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage2a-2-1 Assigning Stage2a cancer for patient ");      
        CancerStage cancerStage = new StageiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
  

// ==========================================================
//
// Stage2a-3	If document has pT2, pN0, pM0, then Stage IIA Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage2a-3-1"
	salience 4450
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T2 (summarizableId == $patientId)
	    Pn0 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage2a-3-1 Assigning Stage2a cancer for patient ");      
        CancerStage cancerStage = new StageiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end

// ==========================================================
//
// Stage2b-1	If document has pT2, pN1, pM0, then Stage IIB Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage2b-1-1"
	salience 4500
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T2 (summarizableId == $patientId)
	    Pn1 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage2b-1-1 Assigning Stage2b cancer for patient ");      
        CancerStage cancerStage = new StageiibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage2b-2	If document has pT3, pN0, pM0, then Stage IIB Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage2b-2-1"
	salience 4550
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T3 (summarizableId == $patientId)
	    Pn0 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage2b-2-1 Assigning Stage2b cancer for patient ");      
        CancerStage cancerStage = new StageiibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end

// ==========================================================
//
// Stage3a-1	If document has pT0, pN2, pM0, then Stage IIIA Breast Cancer
//	
// ==========================================================
rule "004_breastCancerStage Stage3a-1-1"
	salience 4600
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T0 (summarizableId == $patientId)
	    Pn2 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3a-1-1 Assigning Stage3a cancer for patient ");      
        CancerStage cancerStage = new StageiiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage3a-2	If document has pT1, pN2, pM0, then Stage IIIA Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage3a-2-1"
	salience 4650
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T1 (summarizableId == $patientId)
	    Pn2 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3a-2-1 Assigning Stage3a cancer for patient ");      
        CancerStage cancerStage = new StageiiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end

// ==========================================================
//
// Stage3a-3	If document has pT2, pN2, pM0, then Stage IIIA Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage3a-3-1"
	salience 4700
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T2 (summarizableId == $patientId)
	    Pn2 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3a-3-1 Assigning Stage3a cancer for patient ");      
        CancerStage cancerStage = new StageiiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage3a-4	If document has pT3, pN1, pM0, then Stage IIIA Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage3a-4-1"
	salience 4750
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T3 (summarizableId == $patientId)
	    Pn1 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3a-4-1 Assigning Stage3a cancer for patient ");      
        CancerStage cancerStage = new StageiiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage3a-5	If document has pT3, pN2, pM0, then Stage IIIA Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage3a-5-1"
	salience 4800
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T3 (summarizableId == $patientId)
	    Pn2 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3a-5-1 Assigning Stage3a cancer for patient ");      
        CancerStage cancerStage = new StageiiiaImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end

// ==========================================================
//
// Stage3b-1	If document has pT4, pN0, pM0, then Stage IIIB Breast Cancer	
//
// ==========================================================
rule "004_breastCancerStage Stage3b-1-1"
	salience 4800
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T4 (summarizableId == $patientId)
	    Pn0 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3b-1-1 Assigning Stage3b cancer for patient ");      
        CancerStage cancerStage = new StageiiibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage3b-2	If document has pT4, pN1, pM0, then Stage IIIB Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage3b-2-1"
	salience 4850
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T4 (summarizableId == $patientId)
	    Pn1 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3b-2-1 Assigning Stage3b cancer for patient ");      
        CancerStage cancerStage = new StageiiibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
    
// ==========================================================
//
// Stage3b-3	If document has pT4, pN2, pM0, then Stage IIIB Breast Cancer
//
// ==========================================================
 rule "004_breastCancerStage Stage3b-3-1"
	salience 4900
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    T4 (summarizableId == $patientId)
	    Pn2 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3b-3-1 Assigning Stage3b cancer for patient ");      
        CancerStage cancerStage = new StageiiibImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end
 
// ==========================================================
//	
// Stage3c-1	If document has any pT value, pN3, pM0, then Stage IIIC Breast Cancer
//
// ========================================================== 
 rule "004_breastCancerStage Stage3c-1-1"
	salience 4950
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    PrimaryTumorClassification (summarizableId == $patientId)
	    Pn3 (summarizableId == $patientId)
	    M0 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage3c-1-1 Assigning Stage3c cancer for patient ");      
        CancerStage cancerStage = new StageiiicImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end 
    
// ==========================================================
//
// Stage4-1	If document has any pT value, any pN value, pM1, then Stage IV Breast Cancer
//
// ==========================================================
rule "004_breastCancerStage Stage4-1-1"
	salience 5000
	when
	    KbGoal (name == "extract-stage", isActive > 0)
	    KbPatient( $patientId : id )
	    PrimaryTumorClassification (summarizableId == $patientId)
	    ClinicalRegionalLymphNodeClassification (summarizableId == $patientId)
	    M1 (summarizableId == $patientId)
        not CancerStage (summarizableId == $patientId)
    then
        System.out.println("<DROOLS-ENGINE> Stage4-1-1 Assigning Stage4 cancer for patient ");      
        CancerStage cancerStage = new StageivImpl();
        cancerStage.setSummarizableId($patientId);
        insert(cancerStage);
    end   
    
rule "004_breastCancerStage Transition Goal State"
	salience 500
	when
	    $g : KbGoal (name == "extract-stage",
                isActive > 0)
    then
        System.out.println("<DROOLS-ENGINE> Achieved goal " + $g.getName());
    	retract($g);
    end
    


    
