package org.healthnlp.deepphe.summarization.drools.rules;

//list any import classes here.
import org.healthnlp.deepphe.summarization.drools.kb.*;
import org.healthnlp.deepphe.summarization.drools.kb.impl.*;

//
// Receptor status - Hormone receptor negative
//
// If ER-, PR- => phenotype receptor status = Hormone Receptor Negative
//

//
// ER - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with ER relation <ER, value>
//  => phenotype ER = value

rule "005_breastCancerReceptorStatus Er"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         EstrogenReceptorStatus($receptorStatusId : id, summarizableId == $encounterId)
         $interpretation : Interpretation($interpretationId : id, summarizableId == $encounterId)   
         HasInterpretation($receptorStatusId == domainId, $interpretationId == rangeId)    
         not EstrogenReceptorStatus(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Estrogen Receptor Fires... ");
        EstrogenReceptorStatus patientEr = new EstrogenReceptorStatusImpl();
        Interpretation patientInterpretation = beanCopyInterpretation($interpretation);
        HasInterpretation patientRelationHasInterpretation = new HasInterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientEr.getId());
        patientRelationHasInterpretation.setRangeId(patientInterpretation.getId());
        patientEr.setSummarizableId($patientId);
        patientInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
       
        insert(patientEr);
        insert(patientInterpretation);
        insert(patientRelationHasInterpretation);
    end
    
//
// PR - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with PR relation <PR, value>
//  => phenotype PR = value
//
rule "005_breastCancerReceptorStatus Pr"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         ProgesteroneReceptorStatus($prId : id, summarizableId == $encounterId)
         $interpretation : Interpretation($interpretationId : id, summarizableId == $encounterId)   
         HasInterpretation($prId == domainId, $interpretationId == rangeId)    
         not ProgesteroneReceptorStatus(summarizableId == $patientId)          
    then
        System.out.println("<DROOLS-ENGINE> Progesterone Receptor Fires... ");
        ProgesteroneReceptorStatus patientPr = new ProgesteroneReceptorStatusImpl();
        Interpretation patientInterpretation = beanCopyInterpretation($interpretation);
        HasInterpretation patientRelationHasInterpretation = new HasInterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientPr.getId());
        patientRelationHasInterpretation.setRangeId(patientInterpretation.getId());
                       
        patientPr.setSummarizableId($patientId);
        patientInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
            
        insert(patientPr);
        insert(patientInterpretation);
        insert(patientRelationHasInterpretation);
    end
    
//
// Her2 - Pathology Report
//
//  If patient has encounter data "tumor" from Pathology with Her2 relation <Her2, value>
//  => phenotype Her2 = value  
  
rule "005_breastCancerReceptorStatus Her2"
	salience 1000
	when
         KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         KbEncounter($patientId == patientId,
                     $encounterId : id,                          
                     ( kind == "Pathology Report" || kind == "Progress Note" ))
         Her2neuReceptorStatus($herNeuId : id, summarizableId == $encounterId)
         $interpretation : Interpretation($interpretationId : id, summarizableId == $encounterId)   
         HasInterpretation($herNeuId == domainId, $interpretationId == rangeId)    
         not Her2neuReceptorStatus(summarizableId == $patientId)         	           
    then
        System.out.println("<DROOLS-ENGINE> Her2/Neu Receptor Fires... ");
        Her2neuReceptorStatus patientHer2 = new Her2neuReceptorStatusImpl();
        Interpretation patientInterpretation = beanCopyInterpretation($interpretation);
        HasInterpretation patientRelationHasInterpretation = new HasInterpretationImpl();
        patientRelationHasInterpretation.setDomainId(patientHer2.getId());
        patientRelationHasInterpretation.setRangeId(patientInterpretation.getId());
 
        patientHer2.setSummarizableId($patientId);
        patientInterpretation.setSummarizableId($patientId);
        patientRelationHasInterpretation.setSummarizableId($patientId);
 
        insert(patientHer2);
        insert(patientInterpretation);
        insert(patientRelationHasInterpretation);
    end
 
//
// Receptor status triple positive
//
// If ER+, PR+, HER2+ => phenotype receptor status = Triple Positive
//
rule "005_breastCancerReceptorStatus Triple Positive"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Her2neuReceptorStatus($her2NeuId : id, summarizableId == $patientId)         
         Positive($erPosId : id)
         Positive($prPosId : id)
         Positive($her2NeuPosId : id)
         HasInterpretation(domainId == $erId, rangeId == $erPosId)
         HasInterpretation(domainId == $prId, rangeId == $prPosId)
         HasInterpretation(domainId == $her2NeuId, rangeId == $her2NeuPosId)
         not (
              ReceptorStatus($combinedStatusId : id, summarizableId == $patientId)
              and TriplePositive($triplePosId : id, summarizableId == $patientId)    
              and HasInterpretation(domainId == $combinedStatusId, rangeId == $triplePosId, summarizableId == $patientId)
            )                            
    then
        System.out.println("<DROOLS-ENGINE> Triple Positive Fires... ");
 		ReceptorStatus combinedReceptorStatus = new ReceptorStatusImpl();
        TriplePositive combinedTriplePositive = new TriplePositiveImpl();
        HasInterpretation combinedHasInterpretation = new HasInterpretationImpl();
        combinedReceptorStatus.setSummarizableId($patientId);
        combinedTriplePositive.setSummarizableId($patientId);
        combinedHasInterpretation.setSummarizableId($patientId);
        combinedHasInterpretation.setDomainId(combinedReceptorStatus.getId());
        combinedHasInterpretation.setRangeId(combinedTriplePositive.getId());
        insert(combinedReceptorStatus);
        insert(combinedTriplePositive);
        insert(combinedHasInterpretation);
    end
    
//
// Receptor status triple negative
//
// If ER-, PR-, HER2- => phenotype receptor status = Triple Negative
//
rule "005_breastCancerReceptorStatus Triple Negative"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Her2neuReceptorStatus($her2NeuId : id, summarizableId == $patientId)         
         Negative($erNegId : id, summarizableId == $patientId)
         Negative($prNegId : id, summarizableId == $patientId)
         Negative($her2NeuNegId : id, summarizableId == $patientId)
         HasInterpretation(domainId == $erId, rangeId == $erNegId)
         HasInterpretation(domainId == $prId, rangeId == $prNegId)
         HasInterpretation(domainId == $her2NeuId, rangeId == $her2NeuNegId)       
         not (
              ReceptorStatus($combinedStatusId : id, summarizableId == $patientId)
              and TripleNegative($tripleNegId : id, summarizableId == $patientId)    
              and HasInterpretation(domainId == $combinedStatusId, summarizableId == $patientId)
            )                    
    then
        System.out.println("<DROOLS-ENGINE> Triple Negative Fires... ");
        ReceptorStatus combinedReceptorStatus = new ReceptorStatusImpl();
        TripleNegative combinedTripleNegative = new TripleNegativeImpl();
        HasInterpretation combinedHasInterpretation = new HasInterpretationImpl();
        combinedReceptorStatus.setSummarizableId($patientId);
        combinedTripleNegative.setSummarizableId($patientId);
        combinedHasInterpretation.setSummarizableId($patientId);
        combinedHasInterpretation.setDomainId(combinedReceptorStatus.getId());
        combinedHasInterpretation.setRangeId(combinedTripleNegative.getId());
        insert(combinedReceptorStatus);
        insert(combinedTripleNegative);
        insert(combinedHasInterpretation);
    end
    
//
// Receptor status - Hormone receptor negative
//
// If ER-, PR- => phenotype receptor status = Hormone Receptor Negative
//
rule "005_breastCancerReceptorStatus Hormone receptor negative"
	salience 750
	when
	     KbGoal (name == "extract-receptor-status",
                isActive > 0)
         KbPatient($patientId : id)
         EstrogenReceptorStatus($erId : id, summarizableId == $patientId)
         ProgesteroneReceptorStatus($prId : id, summarizableId == $patientId)
         Negative($erPosId : id, summarizableId == $patientId)
         Negative($prPosId : id, summarizableId == $patientId)
         HasInterpretation(domainId == $erId, rangeId == $erPosId)
         HasInterpretation(domainId == $prId, rangeId == $prPosId)
         not TripleNegative(summarizableId == $patientId)                      
    then
        System.out.println("<DROOLS-ENGINE> Hormone receptor negative fires... ");
        TripleNegative patientTripleNegative = new TripleNegativeImpl();
        patientTripleNegative.setSummarizableId($patientId);
        insert(patientTripleNegative);
    end    

//
// Retract the goal for this module
//
rule "005_breastCancerReceptorStatus Transition Goal State"
	salience 500
	when
	    $g : KbGoal (name == "extract-receptor-status",
                isActive > 0)
    then
        System.out.println("<DROOLS-ENGINE> Achieved goal " + $g.getName());
    	retract($g);
    end
    
function Interpretation beanCopyInterpretation(Interpretation srcInterpretation) {
        return (Interpretation) beanCopy(srcInterpretation);
        }    
